configurations {
    libDependencies
    hostDependencies
    bootDependencies
    extensionDependencies
    all {
        transitive = false
    }
}

dependencies {
    libDependencies group: 'org.codehaus.fabric3.spec', name: 'sca-api', version: scaApiVersion
    libDependencies group: servletGroupId, name: servletArtifactId, version: servletVersion
    libDependencies project(':kernel:api:fabric3-api')
    libDependencies project(':kernel:api:fabric3-node-api')
    libDependencies project(':kernel:api:fabric3-model-api')
    libDependencies project(':kernel:api:fabric3-host-api')

    // JSP must be on the host classpath as end-user tags may implement interfaces
    hostDependencies(group: 'org.mortbay.jetty', name: 'jsp-api-2.1', version: jspApiVersion) {
        exclude module: 'servlet-api-2.5'
    }
    hostDependencies group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: jaxRsVersion
    hostDependencies group: 'javax.annotation', name: 'javax.annotation-api', version: annotationsVersion
    hostDependencies group: 'javax.xml.bind', name: 'jaxb-api-osgi', version: jaxbApiVersion
    hostDependencies group: 'javax.xml.stream', name: 'stax-api', version: staxApiVersion
    hostDependencies group: 'org.codehaus.woodstox', name: 'stax2-api', version: stax2ApiVersion
    hostDependencies group: 'org.codehaus.woodstox', name: 'woodstox-core-asl', version: woodstoxVersion

    bootDependencies project(':kernel:impl:fabric3-monitor')
    bootDependencies project(':kernel:impl:fabric3-introspection-java')
    bootDependencies project(':kernel:impl:fabric3-introspection-xml')
    bootDependencies project(':kernel:api:fabric3-spi')
    bootDependencies project(':kernel:impl:fabric3-util')
    bootDependencies project(':kernel:impl:fabric3-pojo')
    bootDependencies project(':kernel:impl:fabric3-jdk-reflection')
    bootDependencies project(':kernel:impl:fabric3-transform')
    bootDependencies project(':kernel:impl:fabric3-policy')
    bootDependencies project(':kernel:impl:fabric3-contribution')
    bootDependencies project(':kernel:impl:fabric3-fabric')
    bootDependencies project(':kernel:impl:fabric3-system')
    bootDependencies project(':kernel:impl:fabric3-thread-pool')

    bootDependencies(group: 'jaxen', name: 'jaxen', version: '1.1.1')

    extensionDependencies(group: 'com.lmax', name: 'disruptor', version: disruptorVersion)

    extensionDependencies project(':extension:management:fabric3-management-rest')
    extensionDependencies project(':extension:other:databinding:fabric3-databinding-json')
    extensionDependencies project(':extension:management:fabric3-management-jmx')
    extensionDependencies project(':extension:management:fabric3-management-jmx-agent')
    extensionDependencies project(':extension:core:fabric3-jdk-proxy')
    extensionDependencies project(':extension:core:fabric3-channel-impl')
    extensionDependencies project(':extension:core:fabric3-channel-disruptor')
    extensionDependencies project(':extension:implementation:fabric3-java')
    extensionDependencies project(':extension:core:fabric3-async')
    extensionDependencies project(':extension:core:fabric3-sca-intents')
    extensionDependencies project(':extension:core:fabric3-jetty')
    extensionDependencies project(':extension:core:fabric3-contribution-scanner')
    extensionDependencies project(':extension:core:fabric3-resource')
    extensionDependencies project(':extension:core:fabric3-execution')
    extensionDependencies project(':extension:other:security:fabric3-security')
    extensionDependencies project(':extension:other:security:fabric3-security-impl')
    extensionDependencies project(':extension:core:fabric3-recovery')
    extensionDependencies project(':extension:federated:fabric3-federation-provisioning')
    extensionDependencies project(':extension:federated:fabric3-federation-allocation')
    extensionDependencies project(':extension:federated:fabric3-federation-deployment')
    extensionDependencies project(':extension:federated:fabric3-federation-node')
    extensionDependencies project(':extension:federated:fabric3-jgroups')
    extensionDependencies project(':extension:federated:fabric3-federation-jgroups')
    extensionDependencies project(':extension:core:fabric3-monitor-spi')
    extensionDependencies project(':extension:core:fabric3-monitor-impl')
    extensionDependencies project(':extension:core:fabric3-monitor-appender')
    extensionDependencies project(':extension:node:fabric3-node-impl')

    configurations.compile.extendsFrom(configurations.libDependencies)
    configurations.compile.extendsFrom(configurations.hostDependencies)
    configurations.compile.extendsFrom(configurations.bootDependencies)
    configurations.compile.extendsFrom(configurations.extensionDependencies)
}


task distZip(type: Zip) {
    archiveName = 'runtime-standalone-' + version + '-bin.zip'
    into('bin') {
        from(project(':runtime:standalone:fabric3-standalone-server').jar).rename({ 'server.jar' })
        fileMode = 0755
    }
    into('bin') {
        from(project(':runtime:standalone:fabric3-standalone-shutdown').jar).rename({ 'shutdown.jar' })
        fileMode = 0755
    }

    into('lib') {
        from configurations.libDependencies
    }

    into('host') {
        from configurations.hostDependencies
    }

    into('boot') {
        from configurations.bootDependencies - configurations.hostDependencies - configurations.libDependencies
    }

    into('extensions') {
        from configurations.extensionDependencies - configurations.bootDependencies - configurations.hostDependencies - configurations.libDependencies
    }

    from { 'src/main' }
}
